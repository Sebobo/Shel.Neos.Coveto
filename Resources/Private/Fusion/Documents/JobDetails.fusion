prototype(Shel.Neos.Coveto:Document.JobDetails) < prototype(Neos.Neos:Page) {
    @context.jobid = ${request.arguments.jobid}
    @context.job = ${request.arguments.jobid ? Shel.Neos.Coveto.getJob(request.arguments.jobid) : null}

    httpResponseHead.statusCode = ${job ? 200 : 404}

    prototype(Neos.Neos:PrimaryContent).covetoJob {
        condition = true
        type = 'Shel.Neos.Coveto:Component.JobDetails'
        @position = 'before default'
    }

    prototype(Neos.Seo:CanonicalLink) {
        attributes.href.renderer.default.renderer.arguments.jobid = ${jobid}
    }

    prototype(Neos.Seo:TitleTag) {
        titleOverride.@process.override = ${job ? job.title : value}
    }

    prototype(Neos.Seo:MetaDescriptionTag) {
        attributes.content.@process.override = ${job ? String.cropAtSentence(job.text, 160) : value}
        @if.isNotBlank = ${request.arguments.jobid || !String.isBlank(q(node).property('metaDescription'))}
    }

    prototype(Neos.Seo:TwitterCard) {
        title.@process.override = ${job ? job.title : value}
        description.@process.override = ${job ? String.cropAtSentence(job.text, 200) : value}
        url.@process.override = Shel.Neos.Coveto:Helper.JobList.UriRenderer {
            detailsPage = ${documentNode}
            id = ${jobid}
            @if.hasJob = ${jobid}
        }
    }

    prototype(Neos.Seo:OpenGraphMetaTags) {
        title.@process.override = ${job ? job.title : value}
        description.@process.override = ${job ? String.cropAtSentence(job.text, 200) : value}
        url.@process.override = Shel.Neos.Coveto:Helper.JobList.UriRenderer {
            detailsPage = ${documentNode}
            id = ${jobid}
            @if.hasJob = ${jobid}
        }
    }

    @cache {
        mode = 'dynamic'
        entryIdentifier {
            static = 'jobDetailsPage'
            node = ${node}
        }
        context {
            node = 'node'
            documentNode = 'documentNode'
            site = 'site'
            jobid = 'jobid'
        }
        entryTags {
            node = ${Neos.Caching.nodeTag(node)}
        }
        entryDiscriminator = ${request.arguments.jobid || true}
        maximumLifetime = 3600
    }
}
