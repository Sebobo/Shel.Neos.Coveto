prototype(Shel.Neos.Coveto:Component.JobDetails) < prototype(Neos.Fusion:Component) {
    jobId = ${request.arguments.jobid}
    locationId = ${q(documentNode).property('locationId')}
    job = ${Shel.Neos.Coveto.getJob(this.jobId)}
    job.@if.hasId = ${this.jobId}

    renderer = Neos.Fusion:Case {
        job {
            condition = ${props.job}
            renderer = Shel.Neos.Coveto:Component.JobDetails.Presentation {
                job = ${props.job}
            }
        }
        default {
            condition = true
            renderer = afx`
                <section class="jobdetails jobdetails--notfound">
                    <p>
                        {I18n.translate('Shel.Neos.Coveto:NodeTypes.Document.JobDetails:message.jobNotFound')}
                    </p>
                    <Shel.Neos.Coveto:Component.JobList locationId={props.locationId} />
                </section>
            `
        }
    }
}
